
<!Doctype: HTML w/ Processing>

<html>

  <head>
      <meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
      <title>A M O E B</title>
      <script type="text/javascript" src="js/processing.js"></script>
  </head>
  
  <body>
    
    <script type="text/processing">
    
     float t;
float vLines=65;
float vSpace=1;
int vShape=10;
float vSpeed=.2;
color vColor = 255;
color vBackground = 0;
int w = 5;
int vRadBig = 260;
int vRadSmall = 60;

final int stateStartMenu = 0;
final int stateRunProgram = 1;
int state = stateStartMenu;

void setup(){size(800, 800); background(0, 10, 20);}

void draw(){  
  if (state == stateStartMenu) {
    translate(width/2, height/2); //center in comp
    textAlign(CENTER, BOTTOM);
    textSize(48);
    text("LINES IN ORBIT", 0, -90);
    textSize(24);
    text("NUMBER KEYS (0-9) change Color", 0, -0);
    text("ARROW KEYS change Speed and Shape", 0, 30);
    text("SHIFT resets Speed", 0, 60);
    text("CONTROL resets Shape", 0, 90);
    text("CLICK to Start", 0, 180);
    noLoop();
  }
  else {
    noFill();
    strokeWeight(w);
    translate(width/2, height/2);
    background(vBackground);
    for(int i=0; i<vLines; i+=vSpace){ //number of lines
      line(x1(t-i), y1(t-i), x2(t-i), y2(t-i)); //draw lines
      line(-x1(-t-i), -y1(-t-i), -x3(-t-i), -y3(-t-i)); //draw lines reversed
      stroke(vColor, 255-4*i); //fade opacity
    }
    t-=vSpeed;  //rotate speed
  }
}

float x1(float t){  //outer point x
  return sin(t/10)*(mouseX/8 + vRadBig);
}

float y1(float t){  //outer point y
  return cos(t/vShape)*(mouseX/8 + vRadBig);
}

float x2(float t){  //inner point x
  return sin(t/vShape+5)*(mouseY/8 +vRadSmall);
}

float y2(float t){  //inner point y
  return cos(t/vShape+5)*(mouseY/8 +vRadSmall);
}

float x3(float t){  //reversed inner point x
  return sin(t/vShape-5)*(mouseY/8 +vRadSmall);
}

float y3(float t){  //reversed inner point y
  return cos(t/vShape-5)*(mouseY/8 +vRadSmall);
}

void mousePressed() {
 if (state == stateStartMenu) {
   state =stateRunProgram;
   loop();
 }
 if (mouseX>390 && mouseX<410 && mouseY > 390 && mouseY < 410 && state == stateRunProgram) {
   vBackground = 255;
   w = 2;
   vRadBig = 600 -mouseX/8;
   vRadSmall = 0 - mouseY/8;
   vColor = color(0, 10, 20);
 }
}

/*
void mouseWheel(MouseEvent event) { //change number of lines
  int e = event.getCount();
  vLines+=e*.8;
  if (vSpace>1){
    vSpace+=e; // -----BROKEN-----
  }
  println(vSpace);
}
*/

void keyPressed() {
//-----SPEED-----
    if (keyCode == UP) { //increase speed
      vSpeed+=0.1; println(vSpeed);
    } else if (keyCode == DOWN) { //decrease speed
      vSpeed-=0.1; println(vSpeed);
    } else if (keyCode == SHIFT) { //reset speed
      vSpeed=0; println(vSpeed);
//-----SHAPE-----
    } else if (keyCode == LEFT && vShape>-25) { //change shape back
      vShape--; println(vShape);
    } else if (keyCode == RIGHT && vShape<25) { //change shape forward
      vShape++; println(vShape);
    } else if (keyCode == CONTROL) { //reset shape
      vShape=10; println(vShape);
//-----COLOR-----
    } else if (key == '0') { 
      vColor = color(255); //white
    } else if (key == '1') {
      vColor = color(255, 150, 150); //red
    } else if (key == '2') {
      vColor = color(250, 210, 150); //orange
    } else if (key == '3') {
      vColor = color(240, 240, 150); //yellow
    } else if (key == '4') {
      vColor = color(150, 255, 150); //green
    } else if (key == '5') {
      vColor = color(150, 240, 240); //bluegreen
    } else if (key == '6') {
      vColor = color(150, 170, 240); //blue
    } else if (key == '7') {
      vColor = color(240, 150, 255); //purple
    } else if (key == '8') {
      vColor = color(250, 150, 220); //pink
    } else if (key == '9') {
      vColor = color(127, 127, 127); //grey
    } else if (keyCode == DELETE) {
      w = 5;
      vRadBig = 260;
      vRadSmall = 60;
      vColor = color(255);
      vBackground = 0;
    }
}

    </script>
    <canvas id="sketch" style="border: 1px solid black;"></canvas>
    
  </body>
  
</html>
