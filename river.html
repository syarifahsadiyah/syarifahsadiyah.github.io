
<!Doctype: HTML w/ Processing>

<html>

  <head>
      <meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
      <title>R I V E R</title>
      <script type="text/javascript" src="js/processing.js"></script>
  </head>
  
  <body>
    
    <script type="text/processing">
    
      //C=0,H=128
//draw=_=>{createCanvas(W=640,W)
//C+=.002
//for(j=0;j<W;j+=H)for(i=0;i<W;i+=H)D(i,j,H)}
//D=(x,y,s)=>{a=x+s/2,b=y+s/2
//abs(noise(a/W,b/W,mag(a,b)/W-C)-.5)>
//map(s,4,H,.05,.18)?
//square(x,y,s):
//(s/=2)>4&&(D(x,y,s),D(x+s,y,s),D(x,y+s,s),D(x+s,y+s,s))}

int Unit;
float Counter;
float MX, MY;

void setup() {
  //fullScreen();
  size(700, 700);
  
  Unit=128; // largest size of a square
  Counter=0;
  stroke(0);
  noFill();
}

void draw() {
  background(255);
  Counter+=0.002;
  MX=mouseX*0.001;
  MY=mouseY*0.001;
  for (int j=0; j<height; j+=Unit) {
    for (int i=0; i<width; i+=Unit) {
      DrawRect(i, j, Unit);
    }
  }
}

void DrawRect(int x, int y, int size) {
  float cx=x+size/2; // center in the square for noise
  float cy=y+size/2;
  float n=noise(cx/width-MX, cy/height-MY, mag(cx, cy)/height-Counter);
  float threshold=map(size, Unit, 4, 0.18, 0.05 );
  if (abs(n-0.5)>threshold) {
    rect(x, y, size, size);
  } else {
    size=size/2;
    if (size>=4) { // smallest size of a square = 4pixels
      DrawRect(x     , y     , size);
      DrawRect(x+size, y     , size);
      DrawRect(x     , y+size, size);
      DrawRect(x+size, y+size, size);
    }
  }
}

    </script>
    <canvas id="sketch" style="border: 1px solid black;"></canvas>
    
  </body>
  
</html>
